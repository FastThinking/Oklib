apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android support
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttploggingVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"

    // RxJava
    compile "io.reactivex:rxjava:$rootProject.rxjavaVersion"
    compile "io.reactivex:rxandroid:$rootProject.rxandroidVersion"
    compile "com.jakewharton.rxbinding:rxbinding:$rootProject.rxbindingVersion"

    // Glide
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    compile "com.github.bumptech.glide:okhttp-integration:$rootProject.glideokhttpVersion"
    //photoview
    compile "com.commit451:PhotoView:$rootProject.photoviewVersion"

    //Butterknife
    compile 'com.jakewharton:butterknife:8.2.1'
    //fragmentation
    compile 'me.yokeyword:fragmentation:0.10.1'
    //permissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.8.0@aar'

    //log
    compile 'com.orhanobut:logger:1.15'

    //activity animation
    compile 'me.immortalz:transitionhelper:1.0.6'

    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    //轮播图
    compile 'com.bigkoo:convenientbanner:2.0.5'
}
def siteUrl = "https://github.com/ddamon/Oklib"
def gitUrl = "https://github.com/ddamon/Oklib.git"
group = "com.oklib"
version = '1.0.3'
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'oklib'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //添加开发者信息
                developers {
                    developer {
                        id 'ddamon'
                        name 'damon'
                        email 'hcjhuanghe@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    classifier 'javadoc'
    from generateJavadocs.destinationDir
}
generateJavadocs {
    options {
        options.addStringOption('Xdoclint:none', '-quiet')
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}
artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'maven'
        name = 'oklib'
        version {
            name = '1.0.3-release'
            desc = "oklib"
            vcsTag = '1.0.3'
        }
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true  //是否发布
        configurations = ['archives']
    }
}